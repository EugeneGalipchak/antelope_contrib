      subroutine mkcov(v,n1v,nrow,nv,s,c,n1c) 
c-------------------------------------------------------------------
c        forms covariance matrix for a generalized inverse solution 
c  derived from a truncated singular value decomposition. 
c 
c  arguments- 
c    v   - nrow by nv array of right singular vectors from svd. 
c          vectors are assumed to be in the rows of v.  only  
c          nv vectors are used to form c. 
c    n1v - first dimension of v in calling program  
c    nrow- number of rows in v = number of columns in matrix  
c          that svd was performed on. 
c    nv  - number of singular vectors to keep.  this normally 
c          would correspond to the number of singular values kept 
c          in a generalized inverse generated by a trucated svd.
c    s   - vector of singular values
c    c   - (nrow by nrow) array to contain calculated covariance matrix 
c    n1c - first dimension of c in calling program. 
c 
c  language - fortran 
c  author   - gary l. pavlis
c  written  - may 1981
c-------------------------------------------------------------------
      integer n1v,nrow,nv,n1c 
      real v(n1v,nv),s(nv),c(n1c,nrow)
c--loop control variables 
      integer i,j 
c--error exit for bad input 
      if((nrow.lt.1).or.(nv.lt.1)) return 
c--in the following the first row of c is used as a work space
c--c(1,1) is then treated separately in loop on label 250 
c--only the upper triangle is calculated in the following do loops
c--lower triangle is produced by a copy operation (cputlt)
      do 200 j=2,nrow 
            call scopy(nv,v(j,1),n1v,c,1) 
            do 100 i=1,nv 
                  c(i,1) = c(i,1)/s(i)**2 
  100       continue  
            do 150 i=1,j
                  c(i,j) = sdot(nv,c,1,v(i,1),n1v)
  150       continue  
  200 continue
      c(1,1) = 0.0
      do 250 i=1,nv 
            c(1,1) = c(1,1) + (v(1,i)/s(i))**2
  250 continue
      call cputlt(c,n1c,nrow) 
      return
      end 

c $Id$ 
